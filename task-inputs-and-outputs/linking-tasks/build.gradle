plugins {
    id 'base'
}

abstract class AddQuotationMarks extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getInputFile()
    @OutputFile
    abstract RegularFileProperty getOutputFile()

    AddQuotationMarks() {
        inputFile.convention(project.layout.projectDirectory.file('quote.txt'))
        outputFile.convention(project.layout.buildDirectory.file('quote-with-quotation-marks.txt'))
    }

    @TaskAction
    void join() {
        outputFile.get().asFile.text = "\"${inputFile.get().asFile.text}\""
    }
}

TaskProvider<AddQuotationMarks> addQuotationMarks = tasks.register('addQuotationMarks', AddQuotationMarks)

abstract class AddQuoteSource extends DefaultTask {
    @Input
    abstract Property<String> getSource()
    @InputFile
    abstract RegularFileProperty getInputFile()
    @OutputFile
    abstract RegularFileProperty getOutputFile()

    AddQuoteSource() {
        outputFile.convention(project.layout.buildDirectory.file('quote-with-source.txt'))
    }

    @TaskAction
    void join() {
        outputFile.get().asFile.text = "${inputFile.get().asFile.text}\n\n${source.get()}"
    }
}

tasks.register('addQuoteSource', AddQuoteSource) {
    source = 'Dr. No (1963)'
    inputFile = addQuotationMarks.get().outputFile
}